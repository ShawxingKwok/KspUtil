import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    alias(libs.plugins.jvm)
    alias(libs.plugins.publish)
}

test {
    useJUnitPlatform()
}

kotlin {
    jvmToolchain(8)
}

dependencies {
    implementation(libs.reflect)
    implementation(libs.ksp)
    implementation(libs.ktUtil)

    testImplementation(libs.ktTest)
}

tasks.withType(KotlinCompile).configureEach{
    kotlinOptions.freeCompilerArgs += [
        "-Xexplicit-api=warning",
        "-Xcontext-receivers",
        "-opt-in=kotlin.contracts.ExperimentalContracts",
        "-opt-in=com.google.devtools.ksp.KspExperimental",
    ]
}

mavenPublishing {
    def isSNAPSHOT = true
    def version = "1.0.2"
    def artifactId = "ksp-util"

    if (isSNAPSHOT) version += "-SNAPSHOT"
    coordinates("io.github.shawxingkwok", artifactId, version)

    pom {
        def repo = "KspUtil"
        name = "KspUtil"
        description = "Personal ksp util"
        inceptionYear = "2023"

        url = "https://github.com/ShawxingKwok/$repo/"
        scm{
            connection = "scm:git:git://github.com/ShawxingKwok/${repo}.git"
            developerConnection = "scm:git:ssh://git@github.com/ShawxingKwok/${repo}.git"
        }
    }
}